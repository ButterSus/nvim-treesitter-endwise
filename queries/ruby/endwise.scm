((module name: (_) @cursor) @endable @indent (#endwise! "end"))
((class name: (_) @cursor superclass: (_)? @cursor) @endable @indent (#endwise! "end"))
((method name: (_) @cursor parameters: (_)? @cursor) @endable @indent (#endwise! "end"))
((singleton_method name: (_) @cursor parameters: (_)? @cursor) @endable @indent (#endwise! "end"))
((while condition: (_) @cursor body: (do ("do")? @cursor) @endable) @indent (#endwise! "end"))
((until condition: (_) @cursor body: (do ("do")? @cursor) @endable) @indent (#endwise! "end"))
((for value: (_) @cursor body: (do ("do")? @cursor) @endable) @indent (#endwise! "end"))
((do_block "do" @cursor parameters: (_)? @cursor) @endable @indent (#endwise! "end"))
((if condition: (_) @cursor) @endable @indent (#endwise! "end"))

((ERROR ("module" @indent . [(constant) (scope_resolution)] @cursor)) (#endwise! "end"))
((ERROR ("class" @indent . [(constant) (scope_resolution)] @cursor . (superclass)? @cursor)) (#endwise! "end"))
((ERROR ("def" @indent . (identifier) @cursor . (method_parameters)? @cursor)) (#endwise! "end"))
((ERROR ("def" @indent . (identifier) . "." . (identifier) @cursor . (method_parameters)? @cursor)) (#endwise! "end"))
((ERROR ("while" @indent . (_) @cursor . "do"? @cursor)) (#endwise! "end"))
((ERROR ("until" @indent . (_) @cursor . "do"? @cursor)) (#endwise! "end"))
((ERROR ("for" @indent . (_) . (in . "in" . (_) @cursor) . "do"? @cursor)) (#endwise! "end"))
((ERROR ("do" @cursor @indent . (block_parameters)? @cursor)) (#endwise! "end"))
((ERROR ("if" @indent . (_) @cursor . (then)? @cursor . ["elsif" (elsif (_))]? @cursor . ["else" (else)]? @cursor)) (#endwise! "end"))
